/*
 * The Compukters Developers
 *
 * Copyright 2026 Vsevolod Petrov (lazyhat)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import net.fabricmc.loom.api.LoomGradleExtensionAPI

plugins {
    id("kotlin-convention")
    id("architectury-plugin")
    id("com.gradleup.shadow")
}

val libs = libsCatalog()
val needsRemap = !pluginManager.hasPlugin("dev.architectury.loom-no-remap")

setLoaderKind(LoaderKind.NEOFORGE)
version = computeModArchiveVersion()

architectury {
    platformSetupLoomIde()
    neoForge()
}

val common = configurations.create("common")
val shadowBundle = configurations.create("shadowBundle")
val devRuntimeBundle =
    configurations.create("devRuntimeBundle") {
        isCanBeConsumed = false
        isCanBeResolved = true
    }

shadowBundle.dependencies.whenObjectAdded {
    if (this is ProjectDependency) {
        val mergedSourceProjectPaths = common.dependencies.withType<ProjectDependency>().map(ProjectDependency::getPath)
        if (path in mergedSourceProjectPaths) return@whenObjectAdded
        val devRuntimeDependency = dependencies.project(mapOf("path" to path)) as ProjectDependency
        devRuntimeDependency.isTransitive = false
        dependencies.add(devRuntimeBundle.name, devRuntimeDependency)
    }
}

configurations {
    compileClasspath { extendsFrom(common) }
    runtimeClasspath { extendsFrom(common) }
}

dependencies {
    add("neoForge", versionLibrary("neoforge"))
    listOf(
        ":native-runtime-api",
        ":core",
        ":compiler-client",
        ":compiler-runtime",
        ":worker-client",
        ":ide-core",
        ":ide-analysis-client",
        ":ide-client",
    ).forEach { projectPath ->
        implementation(project(projectPath))
        shadowBundle(project(projectPath)) { isTransitive = false }
    }
    listOf(
        "kotlin-stdlib",
        "kotlin-logging",
        "kotlinx-coroutines-core",
        "xz",
    ).forEach { alias -> neoForgeImplementation(libs.findLibrary(alias).get()) }
    listOf("tomlj", "antlr4-runtime").forEach { alias ->
        neoForgeRelocatedImplementation(libs.findLibrary(alias).get())
    }
    compileOnly(libs.findLibrary("checker-qual").get())
}

tasks.named<Jar>("jar") {
    archiveClassifier.set("dev")
}

fun com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar.mergeCompuktersRuntime() {
    configurations = listOf(shadowBundle)
    duplicatesStrategy = DuplicatesStrategy.FAIL
    exclude("META-INF/*.SF", "META-INF/*.RSA", "META-INF/*.DSA")
    relocate("org.tomlj", "ru.lazyhat.compukters.internal.vendor.tomlj")
    relocate("org.antlr.v4.runtime", "ru.lazyhat.compukters.internal.vendor.antlr.v4.runtime")
}

val productionJar = tasks.named<com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar>("shadowJar") {
    mergeCompuktersRuntime()
    archiveClassifier.set(if (needsRemap) "shadow-dev" else "")
}

tasks.register<com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar>("developmentModJar") {
    group = "build"
    description = "Builds the named Compukters mod JAR used by dependent development runs."
    from(sourceSets.main.map { it.output })
    mergeCompuktersRuntime()
    archiveFileName.set(COMPUKTERS_DEVELOPMENT_MOD_FILENAME)
    destinationDirectory.set(layout.buildDirectory.dir(COMPUKTERS_DEVELOPMENT_MOD_DIRECTORY))
}

val devRuntimeLibrariesJar =
    tasks.register<com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar>("devRuntimeLibrariesJar") {
        configurations = listOf(devRuntimeBundle)
        archiveClassifier.set("dev-runtime-libraries")
        duplicatesStrategy = DuplicatesStrategy.FAIL
        exclude("META-INF/*.SF", "META-INF/*.RSA", "META-INF/*.DSA")
    }

dependencies.add("forgeRuntimeLibrary", files(devRuntimeLibrariesJar))
addCompuktersNeoForgeDevelopmentRuntime()

extensions.getByType<LoomGradleExtensionAPI>().nestJars(productionJar, configurations.named("include"))

val finalProductionJar: TaskProvider<out Task> =
    if (needsRemap) {
        tasks.named<net.fabricmc.loom.task.RemapJarTask>("remapJar") {
            inputFile.set(productionJar.flatMap { it.archiveFile })
            archiveClassifier.set("")
            addNestedDependencies.set(false)
            dependsOn(productionJar)
        }
    } else {
        productionJar
    }

tasks.named("assemble") {
    dependsOn(finalProductionJar)
}

tasks.register("buildProductionUniversalJar") {
    group = "build"
    description = if (needsRemap) "Build the remapped production mod jar." else "Build the unobfuscated production mod jar."
    dependsOn(finalProductionJar)
}

fun <T : ModuleDependency> DependencyHandler.neoForgeImplementation(dependency: Provider<T>) {
    val resolvedDependency = dependency.get()
    val implementationDependency = create(resolvedDependency) as ModuleDependency
    val includedDependency = create(resolvedDependency)
    implementation(implementationDependency) { isTransitive = false }
    add("include", includedDependency)
}

fun <T : ModuleDependency> DependencyHandler.neoForgeRelocatedImplementation(dependency: Provider<T>) {
    val resolvedDependency = dependency.get()
    val implementationDependency = create(resolvedDependency) as ModuleDependency
    val shadowDependency = create(resolvedDependency) as ModuleDependency
    implementationDependency.isTransitive = false
    shadowDependency.isTransitive = false
    implementation(implementationDependency)
    add("shadowBundle", shadowDependency)
}
